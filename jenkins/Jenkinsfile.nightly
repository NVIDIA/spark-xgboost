#!/usr/bin/groovy
//@Library('shared-libs') _

// -*- mode: groovy -*-
// Jenkins pipeline
// See documents at https://jenkins.io/doc/book/pipeline/jenkinsfile/

import groovy.transform.Field

/* Unrestricted tasks: tasks that do NOT generate artifacts */

// Utility functions
@Field
def utils

def SERVERS_MAP = [
    'Gpuwa': 'https://gpuwa.nvidia.com/artifactory/sw-spark-maven-local',
    'Local': 'http://apt-sh04:8081/repository/xgboost-spark-release'
]

pipeline {
    agent any

    // Setup common job properties
    options {
        ansiColor('xterm')
        timestamps()
        timeout(time: 360, unit: 'MINUTES')
        buildDiscarder(logRotator(numToKeepStr: '10'))
        gitLabConnection('GitLab Master')
    }

    parameters {
        choice(name: 'DEPLOY_TYPE', choices: ['Gpuwa', 'Local'],
            description: 'REPO where to deploy xgboost artifacts')
        string(name: 'REF', defaultValue: 'support-spark3.0', description: 'Commit to build')
    }

    environment {
        LOCAL_ROOT='jenkins/local'
        BUILD_SCRIPT='$LOCAL_ROOT/build-jvm-multi.sh'
        DEPLOY_SCRIPT='$LOCAL_ROOT/deploy-jvm-multi.sh'
        SERVER_URL = "${SERVERS_MAP."$DEPLOY_TYPE"}"
        URM_CREDS = credentials("svcngcc_artifactory")
    }

    triggers {
        cron('H 0 * * *')
    }

    // Build stages
    stages {
        stage('Jenkins: Get sources') {
            steps {
                updateGitlabCommitStatus(name: 'Jenkins CI', state: 'running')
                script {
                    utils = load('tests/ci_build/jenkins_tools_spark.Groovy')
                    utils.checkoutSrcs()
                }
                stash name: 'srcs', excludes: '.git/'
                milestone label: 'Sources ready', ordinal: 1
            }
        }
        stage('Jenkins: Build & Deploy') {
            agent { label 'docker-gpu' }
            environment {
                // Local env for the current docker container
                OUT="$WORKSPACE/out"
                SIGN_FILE='false'
            }
            steps {
                unstash name: 'srcs'
                script {
                    def IMAGE_NAME="urm.nvidia.com/sw-spark-docker/xgboost:dev3-centos7"
                    def DOCKER_CMD="docker --config $WORKSPACE/.docker"
                    sh """
                        echo $URM_CREDS_PSW | $DOCKER_CMD login https://urm.nvidia.com -u $URM_CREDS_USR --password-stdin
                        $DOCKER_CMD pull $IMAGE_NAME
                        $DOCKER_CMD logout https://urm.nvidia.com
                    """
                    docker.image(IMAGE_NAME).inside("--runtime=nvidia \
                        -v /etc/passwd:/etc/passwd:ro -v /etc/group:/etc/group:ro") {
                        sh "scl enable devtoolset-7 '$BUILD_SCRIPT $SIGN_FILE' "
                        retry(3) {
                            sh "bash $DEPLOY_SCRIPT $SIGN_FILE"
                        }
                    }
                }
            }
        }
    }
    post {
        always {
            script {
                if (currentBuild.currentResult == "SUCCESS") {
                    updateGitlabCommitStatus(name: 'Jenkins CI', state: "success")
                    slack("#rapidsai-spark-cicd", "Success", color: "#33CC33")
                } else {
                    updateGitlabCommitStatus(name: 'Jenkins CI', state: "failed")
                    slack("#rapidsai-spark-cicd", "Failed", color: "#FF0000")
                }
            }
        }
    }
}

void slack(Map params = [:], String channel, String message) {
    Map defaultParams = [
            color: "#000000",
            baseUrl: "https://nvidia.slack.com/services/hooks/jenkins-ci/",
            tokenCredentialId: "slack_token"
    ]

    params["channel"] = channel
    params["message"] = "${BUILD_URL}\n" + message

    slackSend(defaultParams << params)
}
